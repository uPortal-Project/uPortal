description = "Apereo uPortal Tools"

ext {
    generatedSourcesDir = file("${buildDir}/generated-sources/javac/main/java")
}

dependencies {
    api project(':uPortal-hibernate')
    api project(':uPortal-io:uPortal-io-core')
    api project(':uPortal-persondir')
    api project(':uPortal-rdbm')

    implementation "commons-cli:commons-cli:${commonsCliVersion}"
    implementation "org.apache.ant:ant:${antVersion}"
    implementation "org.codehaus.groovy:groovy:${groovyVersion}"

    annotationProcessor "org.hibernate:hibernate-jpamodelgen:${hibernateJpamodelgenVersion}" // Required for javac -s
    compileOnly "${servletApiDependency}"
}

/*
 * This section is the key to IDE integration.  IDE will look for source files in both...
 *
 *   - src/main/java
 *   - build/generated-sources/javac/main/java
 */
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir generatedSourcesDir
        }
    }
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(generatedSourcesDir)
}
