plugins {
    id 'org.openrepose.gradle.plugins.jaxb' version '2.5.0'
}

description = 'Apereo uPortal Search API'

ext {
    generatedSourcesDir = "${buildDir}/generated-sources/xjc"
}

dependencies {
    // All 6 of the following are for the 'com.github.jacobono.jaxb' plugin
    jaxb "com.sun.xml.bind:jaxb-xjc:${jaxbVersion}"
    jaxb "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"
    jaxb "javax.xml.bind:jaxb-api:${jaxbVersion}"
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.6.5'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-annotate:0.6.4'
}

/*
 * This section is the key to IDE integration.  IDE will look for source files in both...
 *
 *   - src/main/java
 *   - build/generated-sources/xjc
 */
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir generatedSourcesDir
        }
    }
}

jaxb {
    // Without the 'accessExternalSchema' setting, this module will not build on Java 8
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    xsdDir = "${projectDir}/src/main/resources/xsd"
    bindingsDir = "${projectDir}/src/main/binding"
    bindings = ['bindings.xjb']
    // Not sure of the role of episodes, but it's better to specify a location within build/
    episodesDir = "${buildDir}/schema/episodes"
    xjc {
        taskClassname = 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
        destinationDir = generatedSourcesDir
        producesDir = generatedSourcesDir
    }
}

compileJava.dependsOn(xjc)
