plugins {
    id 'org.openrepose.gradle.plugins.jaxb' version '2.4.1'
}

description = 'Apereo uPortal Search API'

ext {
    generatedSourcesDir = "${buildDir}/generated-sources/xjc"
}

dependencies {
    // All 6 of the following are for the 'com.github.jacobono.jaxb' plugin
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.3.2'
    jaxb 'javax.xml.bind:jaxb-api:2.3.1'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-ant:1.11.1'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.1.0'
}

/*
 * This section is the key to IDE integration.  IDE will look for source files in both...
 *
 *   - src/main/java
 *   - build/generated-sources/xjc
 */
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir generatedSourcesDir
        }
    }
}

jaxb {
    // Without the 'accessExternalSchema' setting, this module will not build on Java 8
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    xsdDir = "${projectDir}/src/main/resources/xsd"
    bindingsDir = "${projectDir}/src/main/binding"
    bindings = ['bindings.xjb']
    // Not sure of the role of episodes, but it's better to specify a location within build/
    episodesDir = "${buildDir}/schema/episodes"
    xjc {
        taskClassname = 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
        destinationDir = generatedSourcesDir
        producesDir = generatedSourcesDir
    }
}

compileJava.dependsOn(xjc)
