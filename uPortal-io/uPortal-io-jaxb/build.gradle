plugins {
    id 'org.openrepose.gradle.plugins.jaxb' version '2.4.1'
}

description = 'Apereo uPortal Import/Export JAXB Data Model'

ext {
    generatedSourcesDir = "${buildDir}/generated-sources/xjc"
}

dependencies {
    // All 7 of the following are for the 'org.openrepose.gradle.plugins.jaxb' plugin
    jaxb "com.sun.xml.bind:jaxb-core:${jaxbVersion}"
    jaxb "com.sun.xml.bind:jaxb-xjc:${jaxbVersion}"
    jaxb "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"
    jaxb "javax.xml.bind:jaxb-api:${jaxbVersion}"
    jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-ant:${jaxb2basicsVersion}"
    jaxb "org.jvnet.jaxb2_commons:jaxb2-basics:${jaxb2basicsVersion}"
    jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-annotate:${jaxb2basicsAnnotateVersion}"
}

/*
 * This section is the key to IDE integration.  IDE will look for source files in both...
 *
 *   - src/main/java
 *   - build/generated-sources/xjc
 */
sourceSets {
    main {
        java {
            // srcDir 'src/main/java'
            srcDir generatedSourcesDir
        }
    }
}

jaxb {
    // Without the 'accessExternalSchema' setting, this module will not build on Java 8
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    xsdDir = "${projectDir}/src/main/resources/xsd"
    bindingsDir = "${projectDir}/src/main/binding"
    bindings = ['bindings.xjb']
    // Not sure of the role of episodes, but it's better to specify a location within build/
    episodesDir = "${buildDir}/schema/episodes"
    xjc {
        taskClassname = 'org.jvnet.jaxb2_commons.xjc.XJC2Task'
        destinationDir = generatedSourcesDir
        producesDir = generatedSourcesDir
    }
}

compileJava.dependsOn(xjc)
