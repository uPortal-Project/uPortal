description = "Apereo uPortal Web"

ext {
    generatedSourcesDir = file("${buildDir}/generated-sources/javac/main/java")
}

dependencies {
    implementation project(':uPortal-api:uPortal-api-rest')
    implementation project(':uPortal-health')
    implementation project(':uPortal-events')
    implementation project(':uPortal-layout:uPortal-layout-impl')
    implementation project(':uPortal-rendering')

    implementation "com.github.ci-rt:google-visualization-java:${googleVisualizationVersion}"
    implementation "joda-time:joda-time:${jodaTimeVersion}"

    testImplementation "org.apache.portals.pluto:pluto-container-api:${plutoVersion}"
    testImplementation "${servletApiDependency}"

    compileOnly "org.apache.portals.pluto:pluto-container-api:${plutoVersion}"
    compileOnly "org.apache.portals.pluto:pluto-container-driver-api:${plutoVersion}"
    compileOnly "org.apache.portals.pluto:pluto-taglib:${plutoVersion}"
    annotationProcessor "org.hibernate:hibernate-jpamodelgen:${hibernateJpamodelgenVersion}" // Required for javac -s
    compileOnly "javax.servlet.jsp:javax.servlet.jsp-api:${jspApiVersion}"
    compileOnly "${portletApiDependency}"
    compileOnly "${servletApiDependency}"
}

/*
 * This section is the key to IDE integration.  IDE will look for source files in both...
 *
 *   - src/main/java
 *   - build/generated-sources/javac/main/java
 */
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir generatedSourcesDir
        }
    }
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(generatedSourcesDir)
}
